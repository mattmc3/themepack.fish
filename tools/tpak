#!/usr/bin/env fish

set -g PRJDIR (path dirname (path resolve (status dirname)))
set -g COLOR_SCHEMES_JSON $PRJDIR/tools/data/iterm2_colorschemes.json

function _tpak_refresh_iterm2
    # pre-reqs
    if not type jq &>/dev/null
        echo >&2 "tpak: Required tool not found 'jq'."
        return 1
    end

    # setup
    set -l iterm2_dir (mktemp -d -t tpak)
    git clone --depth 1 --quiet https://github.com/mbadolato/iTerm2-Color-Schemes $iterm2_dir 2>/dev/null

    # use jq to combine many color scheme files into one json file we can now use
    rm $COLOR_SCHEMES_JSON &>/dev/null
    jq -s '.' $iterm2_dir/windowsterminal/*.json >$COLOR_SCHEMES_JSON

    # cleanup
    test -d $iterm2_dir && rm -rf $iterm2_dir
end

function _gen_colorschemes
    #set -l iterm_scheme_names (jq '.[] | .name' $COLOR_SCHEMES_JSON)
    # jq 'map( .magenta = .purple | .brightMagenta = .brightPurple | .comment = .cursorColor | del(.purple,.brightPurple,.cursorColor))' ./data/test.json
end

function _tpak_list_iterm2_colorschemes
    for file in (jq -r '.[] | .name' $COLOR_SCHEMES_JSON)
        to_snake_case $file
    end
end

function to_snake_case -a str
    # 'Abc DefGhi-(Dark)' -> 'abc_def_ghi_dark'
    string replace -ra 'HaX0R_.+' '\L$0' $str |
    string replace -ra '([a-z0-9])([A-Z])' '$1_$2'|
    string replace -ra '[\s\-\(\)\.]+' '_' |
    string replace -ra '__+' '_' |
    string replace -ra '^_' '' |
    string replace -ra '_$' '' |
    string lower
end

function is_dark_theme --argument-names color
    # https://github.com/mbadolato/iTerm2-Color-Schemes/blob/a914b8aaada2e66b28647d82f937cb807b5dab6f/tools/converter.py#L94-L99\
    set color (string upper $color | string replace '#' '')
    set -l hexcolor \
        (string sub -s 1 -l 2 $color) \
        (string sub -s 3 -l 2 $color) \
        (string sub -s 5 -l 2 $color)

    set r (echo "ibase=16; $hexcolor[1]" | bc)
    set g (echo "ibase=16; $hexcolor[2]" | bc)
    set b (echo "ibase=16; $hexcolor[3]" | bc)

    test (math 0.2126 x $r + 0.7152 x $g + 0.0722 x $b) -lt 40
end

function _tpak
    switch $argv[1]
        case refresh-iterm2
            _tpak_refresh_iterm2 $argv[2..]
        case list-iterm2-color-schemes
            _tpak_list_iterm2_colorschemes $argv[2..]
        case foo
            _gen_colorschemes $argv[2..]
        case *
            echo >&2 "Unrecognized command '$argv[1]'" && return 1
    end
end
_tpak $argv
